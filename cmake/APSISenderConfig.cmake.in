# Copyright (c) Microsoft Corporation. All rights reserved.
# Licensed under the MIT license.

# Exports target APSI::Sender

@PACKAGE_INIT@

include(CMakeFindDependencyMacro)

macro(warning_when_not_quiet msg)
    if(NOT APSISender_FIND_QUIETLY)
        message(WARNING ${msg})
    endif()
endmacro()

macro(status_when_not_quiet msg)
    if(NOT APSISender_FIND_QUIETLY)
        message(STATUS ${msg})
    endif()
endmacro()

macro(find_apsi_dependency dep)
    find_dependency(${dep})
    if(NOT ${dep}_FOUND)
        warning_when_not_quiet("Could not find dependency `${dep}`")
        set(APSISender_FOUND FALSE)
        return()
    endif()
endmacro()

set(APSISender_FOUND FALSE)

# Locate APSICommon dependency
find_apsi_dependency(APSICommon 1.0.0 EXACT REQUIRED)

include(${CMAKE_CURRENT_LIST_DIR}/APSISenderTargets.cmake)

if(TARGET APSI::Sender)
    status_when_not_quiet("APSISender -> Version ${APSISender_VERSION} detected")
    set(APSISender_FOUND TRUE)
else()
    warning_when_not_quiet("APSISender -> NOT FOUND")
    set(APSISender_FOUND FALSE)
endif()
