# Copyright (c) Microsoft Corporation. All rights reserved.
# Licensed under the MIT license.

# Exports target APSI::Common

@PACKAGE_INIT@

include(CMakeFindDependencyMacro)

macro(apsi_find_dependency dep)
    find_dependency(${dep})
    if(NOT ${dep}_FOUND)
        if(NOT APSICommon_FIND_QUIETLY)
            message(WARNING "Could not find dependency `${dep}`")
        endif()
        set(APSICommon_FOUND FALSE)
        return()
    endif()
endmacro()

set(APSICommon_FOUND FALSE)

list(APPEND CMAKE_MODULE_PATH ${CMAKE_CURRENT_LIST_DIR})

if(APSI_USE_ZMQ)
    apsi_find_dependency(ZeroMQ REQUIRED)
    apsi_find_dependency(ZeroMQpp REQUIRED)
endif()

include(${CMAKE_CURRENT_LIST_DIR}/APSICommonTargets.cmake)

if(TARGET APSI::Common)
    if(NOT APSICommon_FIND_QUIETLY)
        message(STATUS "APSICommon -> Version ${APSICommon_VERSION} detected")
    endif()
    set(APSICommon_FOUND TRUE)
else()
    if(NOT APSICommon_FIND_QUIETLY)
        message(WARNING "APSICommon -> NOT FOUND")
    endif()
    set(APSICommon_FOUND FALSE)
endif()
