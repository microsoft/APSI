# Copyright (c) Microsoft Corporation. All rights reserved.
# Licensed under the MIT license.

# Exports target APSI::Common

@PACKAGE_INIT@

include(CMakeFindDependencyMacro)

macro(warning_when_not_quiet msg)
    if(NOT APSICommon_FIND_QUIETLY)
        message(WARNING ${msg})
    endif()
endmacro()

macro(status_when_not_quiet msg)
    if(NOT APSICommon_FIND_QUIETLY)
        message(STATUS ${msg})
    endif()
endmacro()

macro(find_apsi_dependency dep)
    find_dependency(${dep})
    if(NOT ${dep}_FOUND)
        warning_when_not_quiet("Could not find dependency `${dep}`")
        set(APSICommon_FOUND FALSE)
        return()
    endif()
endmacro()

set(APSICommon_FOUND FALSE)

list(APPEND CMAKE_MODULE_PATH ${CMAKE_CURRENT_LIST_DIR})

# Locate all dependencies before setting up target
find_apsi_dependency(SEAL 3.4 REQUIRED)
find_apsi_dependency(Kuku 1.1.1 EXACT REQUIRED)
find_apsi_dependency(MSGSL REQUIRED)
find_apsi_dependency(ZeroMQ REQUIRED)
find_apsi_dependency(ZeroMQpp REQUIRED)
find_apsi_dependency(FourQ REQUIRED)
find_apsi_dependency(log4cplus 3.0 REQUIRED)

include(${CMAKE_CURRENT_LIST_DIR}/APSICommonTargets.cmake)

if(TARGET APSI::Common)
    status_when_not_quiet("APSICommon -> Version ${APSICommon_VERSION} detected")
    set(APSICommon_FOUND TRUE)
else()
    warning_when_not_quiet("APSICommon -> NOT FOUND")
    set(APSICommon_FOUND FALSE)
endif()
