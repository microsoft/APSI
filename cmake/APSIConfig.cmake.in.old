# Copyright (c) Microsoft Corporation. All rights reserved.
# Licensed under the MIT license.

@PACKAGE_INIT@

include(CMakeFindDependencyMacro)

include(${CMAKE_CURRENT_LIST_DIR}/APSITargets.cmake)

macro(apsi_find_dependency dep)
    find_dependency(${dep})
    if(NOT ${dep}_FOUND)
        if(NOT APSICommon_FIND_QUIETLY)
            message(WARNING "Could not find dependency `${dep}`")
        endif()
        set(APSICommon_FOUND FALSE)
        return()
    endif()
endmacro()

# Exports target APSI::Common

set(APSICommon_FOUND FALSE)

list(APPEND CMAKE_MODULE_PATH ${CMAKE_CURRENT_LIST_DIR})

# Locate all dependencies before setting up target
apsi_find_dependency(SEAL 3.4 REQUIRED)
apsi_find_dependency(Kuku 1.1.1 EXACT REQUIRED)
apsi_find_dependency(MSGSL REQUIRED)
apsi_find_dependency(ZeroMQ REQUIRED)
apsi_find_dependency(ZeroMQpp REQUIRED)
apsi_find_dependency(FourQ REQUIRED)
apsi_find_dependency(log4cplus 3.0 REQUIRED)

include(${CMAKE_CURRENT_LIST_DIR}/APSICommonTargets.cmake)

if(TARGET APSI::Common)
    if(NOT APSICommon_FIND_QUIETLY)
        message(STATUS "APSICommon -> Version ${APSICommon_VERSION} detected")
    endif()
    set(APSICommon_FOUND TRUE)
else()
    if(NOT APSICommon_FIND_QUIETLY)
        message(WARNING "APSICommon -> NOT FOUND")
    endif()
    set(APSICommon_FOUND FALSE)
endif()
